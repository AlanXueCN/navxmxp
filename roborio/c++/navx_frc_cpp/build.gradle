plugins {
  id 'cpp'
  id 'edu.wpi.first.wpilib.versioning.WPILibVersioningPlugin' version '2.2'
  id 'edu.wpi.first.NativeUtils' version '2019.5.15'
  id 'edu.wpi.first.GradleVsCode' version '0.9.0'
}

apply from: 'config.gradle'

task copyFirmwareHeaders(type: Copy) {
    into("${projectDir}/include")
    from("${projectDir}/../../../stm32/navx-mxp") {
		include "**/AHRS*.h"
		include "**/IMU*.h"
	}
}

build.dependsOn(copyFirmwareHeaders)

nativeUtils {
  exportsConfigs {
      navx_frc {
      }
  }
}

model {
  components {
    navx_frc(NativeLibrarySpec) {
      appendDebugPathToBinaries(binaries)
      binaries.all {
        if (it.targetPlatform.name == nativeUtils.wpi.platforms.roborio) {
          it.sources {
            athenaCpp(CppSourceSet) {
              source {
                srcDirs = ['src']
                include '**/*.cpp'
              }
              exportedHeaders {
                srcDirs 'include'
              }
            }
          }
        } else if (it.targetPlatform.name == nativeUtils.wpi.platforms.raspbian) {
          it.sources {
            raspberryCpp(CppSourceSet) {
              source {
                srcDirs = ['raspberry']
                include '**/*.cpp'
              }
              exportedHeaders {
                srcDirs 'include'
              }
            }
          }
        } else {
          it.sources {
            desktopCpp(CppSourceSet) {
              source {
                srcDirs = ['src']
                include '**/*.cpp'
              }
              exportedHeaders {
                srcDirs 'include'
              }
            }
          }
        }
      }
      nativeUtils.useRequiredLibrary(it, "wpilib_shared")
    }
  }
}

apply from: 'publish.gradle'

wrapper {
  gradleVersion = '5.4.1'
}
